# G-6020: Try to use output bind arguments in the RETURNING INTO clause of dynamic DML statements rather than the USING clause. 

!!! tip "Minor"
    Maintainability

## Reason

When a dynamic `INSERT`, `UPDATE`, or `DELETE` statement has a `RETURNING` clause, output bind arguments can go in the `RETURNING INTO` clause or in the USING clause.

You should use the `RETURNING INTO` clause for values returned from a DML operation. Reserve `OUT` and `IN OUT` bind variables for dynamic PL/SQL blocks that return values in PL/SQL variables.


## Example (bad)

``` sql
create or replace package body employee_api is
   procedure upd_salary (in_employee_id  in     employees.employee_id%type
                        ,in_increase_pct in     types_up.percentage
                        ,out_new_salary     out employees.salary%type)
   is
      co_sql_stmt constant types_up.big_string_type := '
           UPDATE employees SET salary = salary + (salary / 100 * :1) 
            WHERE employee_id = :2
        RETURNING salary INTO :3';
   begin
     execute immediate co_sql_stmt
           using in_increase_pct, in_employee_id, out out_new_salary;
   end upd_salary;
end employee_api;
/
```

## Example (good)

``` sql
create or replace package body employee_api is
   procedure upd_salary (in_employee_id  in     employees.employee_id%type
                        ,in_increase_pct in     types_up.percentage
                        ,out_new_salary     out employees.salary%type)
   is
      co_sql_stmt constant types_up.big_string_type := 
          'UPDATE employees SET salary = salary + (salary / 100 * :1) 
            WHERE employee_id = :2
        RETURNING salary INTO :3';
   begin
     execute immediate co_sql_stmt
           using in_increase_pct, in_employee_id
           returning into out_new_salary;
   end upd_salary;
end employee_api;
/
```